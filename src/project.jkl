#DEFINE BLD_BITS 32
#DEFINE ARCHITECTURE "fox32"
#INCLUDE "sys/rtl.hjk" // Printing functionality
#INCLUDE "sys/os.hjk" // fox32rom bindings
#INCLUDE "sys/vec.hjk"
#INCLUDE "project.hjk"

PUBLIC PROJECT_WINDOW: Window
VecOf(ProjFile,VecProjFile,VecProjFile_WithCapacity,VecProjFile_Null,VecProjFile_Free,VecProjFile_Grow,VecProjFile_ReserveOne,VecProjFile_ReserveAt,VecProjFile_Remove)
PUBLIC project_files: VecProjFile

#ASM [
menu_bar:
.global menu_bar
    .db 2
    .dp menu_project_items
    .dp menu_project_name
    .dp menu_help_items
    .dp menu_help_name
menu_project_name:
    .db 7 .ds "Project" .db 0
menu_project_items:
    .db 7
    .db 12
    .db 11 .ds "New Project" .db 0
    .db 12 .ds "Open Project" .db 0
    .db 4 .ds "Save" .db 0
    .db 8 .ds "Add File" .db 0
    .db 11 .ds "Remove File" .db 0
    .db 5 .ds "Build" .db 0
    .db 4 .ds "Run" .db 0
menu_help_name:
    .db 4 .ds "Help" .db 0
menu_help_items:
    .db 1
    .db 16
    .db 16 .ds "About Fox Studio" .db 0
]

FN GetNameFromPath(IN str: ^UBYTE): ^UBYTE
    len := RtlMeasureString(str)
    IF len == 0 THEN RETURN str END
    ind: UWORD = (CAST str TO UWORD)+(len-1)
    WHILE ind >= (CAST str TO UWORD) DO
        IF (CAST ind TO ^UBYTE)^ == '/' THEN
            RETURN CAST (ind + 1) TO ^UBYTE
        END
        ind -= 1
    END
    RETURN str
END

EXTERN FN DisplaySplash(IN startup: UBYTE)
FN HandleMenuClick (
    IN menu : UWORD,
    IN item : UWORD,
)
    IF menu == 1 THEN // Help Menu
        CloseMenu(&menu_bar)
        DisplaySplash(FALSE)
    END
END

FN Project_InitWindow()
    NewWindow(&PROJECT_WINDOW, "Fox Studio", 192, 256, 32, 32, &menu_bar, NULLPTR)
    VecProjFile_WithCapacity(4,&project_files)
    Project_RefreshWindow()
END

FN Project_RefreshWindow()
    DrawFilledRectangleToOverlay(0,16,192,256,0xff1f1f1f,PROJECT_WINDOW.Overlay)
    DrawStrToOverlay("example.fsp", (192/2)-(11*4), 16, 0xffffffff, 0xff1f1f1f, PROJECT_WINDOW.Overlay)
    DrawFilledRectangleToOverlay(0,33,192,1,0xffffffff,PROJECT_WINDOW.Overlay)
    i := 0
    WHILE i < project_files.Length DO
        path := CAST &project_files.Elems[i].Path TO ^UBYTE
        DrawStrToOverlay(GetNameFromPath(path),0,34,0xffffffff,0xff1f1f1f,PROJECT_WINDOW.Overlay)
        i += 1
    END
END

FN Project_Cleanup()
    DestroyWindow(&PROJECT_WINDOW)
    VecProjFile_Free(&project_files)
END